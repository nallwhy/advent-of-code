# Day 2: Rock Paper Scissors

```elixir
Mix.install([:kino])

input = Kino.Input.textarea("Please paste your input:")
```

## Part 1

[![Run in Livebook](https://livebook.dev/badge/v1/blue.svg)](https://livebook.dev/run?url=https%3A%2F%2Fgithub.com%2Fnallwhy%2Fadvent-of-code%2Fblob%2Fmain%2F2022%2Fday_02.livemd)

The Elves begin to set up camp on the beach. To decide whose tent gets to be closest to the snack storage, a giant Rock Paper Scissors tournament is already in progress.

Rock Paper Scissors is a game between two players. Each game contains many rounds; in each round, the players each simultaneously choose one of Rock, Paper, or Scissors using a hand shape. Then, a winner for that round is selected: Rock defeats Scissors, Scissors defeats Paper, and Paper defeats Rock. If both players choose the same shape, the round instead ends in a draw.

Appreciative of your help yesterday, one Elf gives you an **encrypted strategy guide** (your puzzle input) that they say will be sure to help you win. "The first column is what your opponent is going to play: `A` for Rock, `B` for Paper, and `C` for Scissors. The second column--" Suddenly, the Elf is called away to help with someone's tent.

The second column, you reason, must be what you should play in response: `X` for Rock, `Y` for Paper, and `Z` for Scissors. Winning every time would be suspicious, so the responses must have been carefully chosen.

The winner of the whole tournament is the player with the highest score. Your **total score** is the sum of your scores for each round. The score for a single round is the score for the **shape you selected** (1 for Rock, 2 for Paper, and 3 for Scissors) plus the score for the **outcome of the round** (0 if you lost, 3 if the round was a draw, and 6 if you won).

Since you can't be sure if the Elf is trying to help you or trick you, you should calculate the score you would get if you were to follow the strategy guide.

For example, suppose you were given the following strategy guide:

```
A Y
B X
C Z
```

This strategy guide predicts and recommends the following:

- In the first round, your opponent will choose Rock (`A`), and you should choose Paper (`Y`). This ends in a win for you with a score of **8** (2 because you chose Paper + 6 because you won).
- In the second round, your opponent will choose Paper (`B`), and you should choose Rock (`X`). This ends in a loss for you with a score of 1 (1 + 0).
- The third round is a draw with both players choosing Scissors, giving you a score of 3 + 3 = 6.

In this example, if you were to follow the strategy guide, you would get a total score of `15` (8 + 1 + 6).

**What would your total score be if everything goes exactly according to your strategy guide?**

```elixir
data =
  input
  |> Kino.Input.read()
  |> String.split("\n")
  |> Enum.reject(&(&1 == ""))
  |> Enum.map(&(&1 |> String.split(" ")))
```

```elixir
calc_shape_score = fn
  "X" -> 1
  "Y" -> 2
  "Z" -> 3
end

get_outcome = fn
  "A", "X" -> :draw
  "A", "Y" -> :won
  "A", "Z" -> :lost
  "B", "X" -> :lost
  "B", "Y" -> :draw
  "B", "Z" -> :won
  "C", "X" -> :won
  "C", "Y" -> :lost
  "C", "Z" -> :draw
end

calc_outcome_score = fn
  :lost -> 0
  :draw -> 3
  :won -> 6
end

calc_score = fn opponent_shape, player_shape ->
  calc_shape_score.(player_shape) +
    calc_outcome_score.(get_outcome.(opponent_shape, player_shape))
end

_answer1 =
  data
  |> Enum.map(fn [opponent_shape, player_shape] -> calc_score.(opponent_shape, player_shape) end)
  |> Enum.sum()
```

## Part 2

The Elf finishes helping with the tent and sneaks back over to you. "Anyway, the second column says how the round needs to end: `X` means you need to lose, `Y` means you need to end the round in a draw, and `Z` means you need to win. Good luck!"

The total score is still calculated in the same way, but now you need to figure out what shape to choose so the round ends as indicated. The example above now goes like this:

- In the first round, your opponent will choose Rock (`A`), and you need the round to end in a draw (`Y`), so you also choose Rock. This gives you a score of 1 + 3 = **4**.
- In the second round, your opponent will choose Paper (`B`), and you choose Rock so you lose (`X`) with a score of 1 + 0 = **1**.
- In the third round, you will defeat your opponent's Scissors with Rock for a score of 1 + 6 = **7**.

Now that you're correctly decrypting the ultra top secret strategy guide, you would get a total score of **12**.

Following the Elf's instructions for the second column, **what would your total score be if everything goes exactly according to your strategy guide?**

```elixir
trans_outcome = fn
  "X" -> :lost
  "Y" -> :draw
  "Z" -> :won
end

get_player_shape = fn
  "A", :lost -> "Z"
  "A", :draw -> "X"
  "A", :won -> "Y"
  "B", :lost -> "X"
  "B", :draw -> "Y"
  "B", :won -> "Z"
  "C", :lost -> "Y"
  "C", :won -> "X"
  "C", :draw -> "Z"
end

calc_score = fn opponent_shape, raw_outcome ->
  outcome = trans_outcome.(raw_outcome)
  player_shape = get_player_shape.(opponent_shape, outcome)

  calc_shape_score.(player_shape) + calc_outcome_score.(outcome)
end

_answer2 =
  data
  |> Enum.map(fn [opponent_shape, raw_outcome] -> calc_score.(opponent_shape, raw_outcome) end)
  |> Enum.sum()
```

## Better Solution

```elixir
# Record result set
results = [
  {:rock, :rock, :draw},
  {:rock, :paper, :won},
  {:rock, :scissors, :lost},
  {:paper, :rock, :lost},
  {:paper, :paper, :draw},
  {:paper, :scissors, :won},
  {:scissors, :rock, :won},
  {:scissors, :paper, :lost},
  {:scissors, :scissors, :draw}
]

decode = fn
  input when input in ["A", "X"] -> :rock
  input when input in ["B", "Y"] -> :paper
  input when input in ["C", "Z"] -> :scissors
end

calc_shape_score = fn
  :rock -> 1
  :paper -> 2
  :scissors -> 3
end

calc_outcome_score = fn
  :lost -> 0
  :draw -> 3
  :won -> 6
end

# Part 1

get_outcome = fn opponent_shape, player_shape ->
  results
  |> Enum.find_value(fn
    {^opponent_shape, ^player_shape, outcome} -> outcome
    _ -> nil
  end)
end

calc_score = fn raw_opponent_shape, raw_player_shape ->
  opponent_shape = decode.(raw_opponent_shape)
  player_shape = decode.(raw_player_shape)

  calc_shape_score.(player_shape) +
    calc_outcome_score.(get_outcome.(opponent_shape, player_shape))
end

answer1 =
  data
  |> Enum.map(fn [raw_opponent_shape, raw_player_shape] ->
    calc_score.(raw_opponent_shape, raw_player_shape)
  end)
  |> Enum.sum()

# Part 2

trans_outcome = fn
  "X" -> :lost
  "Y" -> :draw
  "Z" -> :won
end

get_player_shape = fn opponent_shape, outcome ->
  results
  |> Enum.find_value(fn
    {^opponent_shape, player_shape, ^outcome} -> player_shape
    _ -> nil
  end)
end

calc_score = fn raw_opponent_shape, raw_outcome ->
  opponent_shape = decode.(raw_opponent_shape)
  outcome = trans_outcome.(raw_outcome)

  player_shape = get_player_shape.(opponent_shape, outcome)

  calc_shape_score.(player_shape) + calc_outcome_score.(outcome)
end

answer2 =
  data
  |> Enum.map(fn [raw_opponent_shape, raw_outcome] ->
    calc_score.(raw_opponent_shape, raw_outcome)
  end)
  |> Enum.sum()

{answer1, answer2}
```
