<!-- livebook:{"persist_outputs":true} -->

# Day 2: Dive!

## Setup

```elixir
Mix.install([{:nx, github: "elixir-nx/nx", sparse: "nx"}])
```

```output
:ok
```

```elixir
inputs =
  File.read!("./2021/day_02_input.txt")
  |> String.split("\n")
  |> Enum.reject(&(&1 == ""))
  |> Enum.map(fn input ->
    [direction, n_string] =
      input
      |> String.split()

    [direction, _n = String.to_integer(n_string)]
  end)
```

```output
[
  ["forward", 1],
  ["forward", 8],
  ["down", 9],
  ["forward", 3],
  ["down", 6],
  ["down", 1],
  ["down", 1],
  ["forward", 5],
  ["forward", 5],
  ["forward", 8],
  ["down", 6],
  ["forward", 7],
  ["down", 3],
  ["down", 4],
  ["up", 1],
  ["forward", 7],
  ["forward", 5],
  ["forward", 8],
  ["down", 1],
  ["forward", 9],
  ["forward", 9],
  ["forward", 7],
  ["up", 8],
  ["forward", 5],
  ["forward", 9],
  ["down", 3],
  ["forward", 2],
  ["down", 5],
  ["down", 4],
  ["up", 2],
  ["up", 5],
  ["forward", 3],
  ["forward", 8],
  ["down", 9],
  ["down", 1],
  ["forward", 2],
  ["forward", 6],
  ["up", 2],
  ["up", 9],
  ["forward", 8],
  ["down", 7],
  ["forward", 2],
  ["up", 5],
  ["forward", 7],
  ["down", 9],
  ["forward", 9],
  ["forward", 5],
  ["down", 8],
  ["down", ...],
  [...],
  ...
]
```

## Part 1

https://adventofcode.com/2021/day/2

### Part 1

Now, you need to figure out how to pilot this thing.

It seems like the submarine can take a series of commands like `forward 1`, `down 2`, or `up 3`:

* `forward X` increases the horizontal position by X units.
* `down X` **increases** the depth by X units.
* `up X` **decreases** the depth by X units.

Note that since you're on a submarine, `down` and `up` affect your **depth**, and so they have the opposite result of what you might expect.

The submarine seems to already have a planned course (your puzzle input). You should probably figure out where it's going. For example:

```
forward 5
down 5
forward 8
up 3
down 8
forward 2
```

Your horizontal position and depth both start at `0`. The steps above would then modify them as follows:

* `forward 5` adds `5` to your horizontal position, a total of `5`.
* `down 5` adds `5` to your depth, resulting in a value of `5`.
* `forward 8` adds `8` to your horizontal position, a total of `13`.
* `up 3` decreases your depth by `3`, resulting in a value of `2`.
* `down 8` adds `8` to your depth, resulting in a value of `10`.
* `forward 2` adds `2` to your horizontal position, a total of `15`.

After following these instructions, you would have a horizontal position of `15` and a depth of `10`. (Multiplying these together produces **`150`**.)

Calculate the horizontal position and depth you would have after following the planned course. **What do you get if you multiply your final horizontal position by your final depth?**

<!-- livebook:{"branch_parent_index":1} -->

## Solve: Part 1

```elixir
{horizontal_position, depth} =
  inputs
  |> Enum.reduce({0, 0}, fn [direction, n], {horizontal_position, depth} ->
    case direction do
      "forward" -> {horizontal_position + n, depth}
      "down" -> {horizontal_position, depth + n}
      "up" -> {horizontal_position, depth - n}
    end
  end)

_answer = horizontal_position * depth
```

```output
2272262
```

## Solve: Part 1 - Nx

https://github.com/josevalim/aoc/blob/main/2021/day-02.livemd

```elixir
defmodule Day2.Part1.Nx do
  import Nx.Defn

  defn run(tensor) do
    {length, _} = Nx.shape(tensor)

    {_, horizontal_position, depth, _} =
      while {i = 0, horizontal_position = 0, depth = 0, tensor}, i < length do
        direction = tensor[i][0]
        n = tensor[i][1]

        {next_horizontal_position, next_depth} =
          cond do
            direction == 0 -> {horizontal_position + n, depth}
            direction == 1 -> {horizontal_position, depth + n}
            direction == 2 -> {horizontal_position, depth - n}
            :otherwise -> {horizontal_position, depth}
          end

        {i + 1, next_horizontal_position, next_depth, tensor}
      end

    horizontal_position * depth
  end
end

inputs
|> Enum.map(fn [direction, n] ->
  direction_num =
    case direction do
      "forward" -> 0
      "down" -> 1
      "up" -> 2
    end

  [direction_num, n]
end)
|> Nx.tensor()
|> Day2.Part1.Nx.run()
|> Nx.to_scalar()
```

```output
2272262
```

## Part 2

https://adventofcode.com/2021/day/2#part2

### Part Two

Based on your calculations, the planned course doesn't seem to make any sense. You find the submarine manual and discover that the process is actually slightly more complicated.

In addition to horizontal position and depth, you'll also need to track a third value, **aim**, which also starts at 0. The commands also mean something entirely different than you first thought:

* `down X` **increases** your aim by `X` units.
* `up X` **decreases** your aim by `X` units.
* `forward X` does two things:
  * It increases your horizontal position by `X` units.
  * It increases your depth by your aim **multiplied by** `X`.

Again note that since you're on a submarine, `down` and `up` do the opposite of what you might expect: "down" means aiming in the positive direction.

Now, the above example does something different:

* `forward 5` adds `5` to your horizontal position, a total of `5`. Because your aim is `0`, your depth does not change.
* `down 5` adds `5` to your aim, resulting in a value of `5`.
* `forward 8` adds `8` to your horizontal position, a total of `13`. Because your aim is `5`, your depth increases by `8*5=40`.
* `up 3` decreases `y`our aim by `3`, resulting in a value of `2`.
* `down 8` adds `8` to your aim, resulting in a value of `10`.
* `forward 2` adds `2` to your horizontal position, a total of `15`. Because your aim is `10`, your depth increases by `2*10=20` to a total of `60`.

After following these new instructions, you would have a horizontal position of `15` and a depth of `60`. (Multiplying these produces **`900`**.)

Using this new interpretation of the commands, calculate the horizontal position and depth you would have after following the planned course. **What do you get if you multiply your final horizontal position by your final depth?**

## Solve: Part 2

```elixir
{horizontal_position, depth, _aim} =
  inputs
  |> Enum.reduce({0, 0, 0}, fn [direction, n], {horizontal_position, depth, aim} ->
    case direction do
      "forward" -> {horizontal_position + n, depth + aim * n, aim}
      "down" -> {horizontal_position, depth, aim + n}
      "up" -> {horizontal_position, depth, aim - n}
    end
  end)

_answer = horizontal_position * depth
```

```output
2134882034
```
