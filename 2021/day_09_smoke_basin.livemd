<!-- livebook:{"persist_outputs":true} -->

# Day 9: Smoke Basin

## Setup

```elixir
inputs =
  File.read!("./2021/day_09_input.txt")
  |> String.split("\n", trim: true)
  |> Enum.map(fn line ->
    line |> String.split("", trim: true) |> Enum.map(&String.to_integer/1)
  end)
```

```output
[
  [2, 1, 2, 7, 8, 9, 7, 6, 7, 8, 9, 9, 8, 6, 7, 6, 8, 9, 4, 3, 1, 3, 9, 8, 7, 6, 4, 3, 1, 3, 4, 7,
   8, 9, 8, 7, 6, 5, 4, 3, 4, 3, 4, 9, 8, 7, 6, 7, 8, ...],
  [1, 0, 5, 6, 7, 8, 9, 5, 6, 9, 8, 7, 6, 5, 4, 5, 6, 7, 9, 5, 2, 4, 5, 9, 8, 8, 5, 4, 5, 4, 5, 6,
   8, 9, 7, 6, 5, 4, 3, 2, 1, 2, 3, 9, 9, 6, 5, 6, ...],
  [2, 2, 4, 5, 8, 9, 3, 4, 5, 8, 9, 8, 9, 4, 3, 4, 7, 8, 8, 9, 6, 5, 6, 9, 9, 9, 6, 7, 8, 7, 6, 7,
   8, 9, 8, 9, 7, 6, 4, 3, 4, 3, 9, 8, 7, 5, 4, ...],
  [9, 4, 9, 9, 9, 1, 2, 9, 6, 7, 8, 9, 8, 3, 2, 3, 5, 6, 7, 9, 7, 6, 9, 8, 9, 8, 7, 8, 9, 8, 7, 8,
   9, 5, 9, 9, 9, 6, 5, 4, 5, 4, 5, 9, 9, 7, ...],
  [8, 9, 7, 8, 9, 0, 9, 8, 9, 8, 9, 8, 7, 4, 5, 6, 9, 7, 8, 9, 8, 9, 8, 7, 6, 9, 8, 9, 9, 9, 8, 9,
   5, 4, 9, 8, 9, 8, 9, 7, 6, 5, 6, 7, 9, ...],
  [7, 5, 6, 7, 8, 9, 9, 7, 8, 9, 8, 7, 6, 5, 6, 7, 8, 9, 9, 9, 9, 8, 9, 6, 5, 3, 9, 9, 9, 6, 9, 8,
   9, 3, 9, 7, 6, 9, 9, 8, 7, 6, 7, 9, ...],
  [4, 3, 4, 8, 9, 9, 8, 6, 7, 8, 9, 8, 8, 9, 9, 8, 9, 6, 7, 8, 9, 7, 8, 9, 4, 2, 1, 9, 8, 5, 6, 7,
   8, 9, 9, 8, 4, 5, 8, 9, 9, 8, 9, ...],
  [5, 4, 5, 7, 8, 9, 7, 5, 6, 7, 8, 9, 9, 6, 5, 9, 4, 5, 6, 9, 8, 6, 7, 8, 9, 9, 9, 8, 7, 4, 5, 6,
   9, 6, 9, 4, 3, 4, 6, 7, 8, 9, ...],
  [6, 6, 6, 7, 8, 9, 5, 4, 5, 6, 7, 8, 9, 5, 4, 2, 3, 9, 9, 7, 6, 5, 4, 9, 8, 8, 9, 9, 5, 3, 4, 7,
   8, 5, 8, 9, 2, 3, 5, 6, 7, ...],
  [7, 7, 7, 8, 9, 4, 3, 3, 4, 5, 8, 9, 5, 4, 2, 1, 9, 8, 9, 8, 7, 3, 3, 4, 7, 6, 7, 8, 9, 2, 1, 2,
   3, 4, 7, 8, 9, 4, 6, 7, ...],
  [9, 8, 9, 9, 3, 2, 1, 2, 5, 6, 7, 8, 9, 5, 9, 2, 9, 7, 6, 4, 3, 2, 1, 3, 4, 5, 6, 9, 4, 3, 2, 3,
   4, 5, 6, 7, 8, 9, 8, ...],
  [2, 9, 9, 9, 4, 3, 2, 3, 4, 7, 8, 9, 9, 9, 8, 9, 8, 7, 6, 5, 4, 1, 0, 1, 5, 6, 7, 8, 9, 4, 3, 4,
   5, 7, 9, 8, 9, 9, ...],
  [1, 3, 9, 7, 9, 5, 4, 4, 5, 9, 9, 9, 9, 8, 7, 5, 9, 8, 7, 7, 6, 3, 2, 3, 7, 7, 8, 9, 6, 5, 5, 6,
   9, 8, 9, 9, 9, ...],
  [2, 9, 8, 6, 8, 9, 6, 5, 9, 8, 9, 9, 9, 9, 6, 4, 3, 9, 9, 8, 5, 4, 3, 4, 6, 8, 9, 8, 7, 6, 7, 7,
   8, 9, 4, 3, ...],
  [9, 9, 9, 5, 7, 8, 9, 9, 8, 7, 9, 8, 9, 5, 4, 3, 2, 9, 8, 7, 6, 5, 6, 5, 7, 8, 9, 9, 8, 9, 8, 9,
   9, 4, 3, ...],
  [8, 8, 5, 4, 6, 9, 9, 8, 7, 6, 5, 7, 8, 9, 5, 4, 3, 6, 9, 9, 8, 7, 8, 6, 7, 9, 9, 8, 9, 7, 9, 4,
   9, 8, ...],
  [7, 6, 5, 3, 9, 8, 6, 5, 4, 3, 4, 5, 7, 9, 6, 5, 4, 5, 6, 9, 9, 8, 9, 7, 8, 9, 9, 7, 6, 5, 4, 3,
   4, ...],
  [8, 7, 6, 2, 3, 9, 9, 8, 7, 4, 6, 6, 8, 9, 9, 6, 7, 8, 9, 8, 9, 9, 9, 8, 9, 8, 9, 8, 7, 6, 3, 2,
   ...],
  [9, 8, 2, 1, 2, 9, 8, 7, 6, 5, 9, 7, 8, 9, 8, 9, 8, 9, 9, 7, 8, 9, 4, 9, 8, 7, 9, 9, 8, 2, 1, ...],
  [5, 4, 3, 2, 3, 4, 9, 8, 7, 6, 7, 8, 9, 6, 6, 7, 9, 8, 7, 6, 8, 9, 2, 3, 4, 6, 7, 9, 9, 3, ...],
  [7, 6, 5, 3, 6, 5, 6, 9, 8, 7, 8, 9, 6, 5, 4, 9, 8, 7, 6, 5, 1, 0, 1, 3, 4, 5, 6, 9, 8, ...],
  [8, 9, 9, 5, 7, 6, 7, 9, 9, 8, 9, 9, 7, 4, 3, 2, 9, 8, 9, 4, 3, 2, 2, 4, 9, 7, 8, 9, ...],
  [9, 9, 8, 9, 8, 9, 8, 9, 9, 9, 9, 8, 7, 6, 4, 3, 9, 8, 7, 5, 4, 3, 6, 9, 8, 9, 9, ...],
  [7, 8, 7, 8, 9, 9, 9, 9, 9, 7, 8, 9, 8, 9, 5, 4, 5, 9, 9, 6, 5, 4, 5, 6, 7, 8, ...],
  [6, 5, 6, 7, 8, 9, 9, 8, 7, 6, 7, 8, 9, 8, 6, 7, 6, 9, 8, 7, 6, 7, 8, 9, 8, ...],
  [5, 4, 5, 6, 7, 8, 9, 8, 6, 5, 7, 9, 8, 9, 8, 8, 7, 8, 9, 8, 7, 8, 9, 1, ...],
  [6, 5, 7, 8, 9, 9, 8, 9, 7, 4, 5, 6, 7, 8, 9, 9, 8, 9, 4, 9, 8, 9, 3, ...],
  [7, 9, 8, 9, 9, 8, 7, 8, 9, 6, 6, 9, 8, 9, 9, 9, 9, 2, 3, 8, 9, 9, ...],
  [8, 9, 9, 9, 8, 7, 6, 7, 9, 7, 8, 9, 9, 8, 8, 9, 2, 1, 2, 6, 8, ...],
  [9, 4, 5, 9, 8, 6, 5, 9, 8, 9, 9, 8, 7, 6, 7, 8, 9, 2, 3, 5, ...],
  [1, 2, 9, 7, 6, 5, 4, 5, 7, 8, 9, 7, 6, 5, 6, 7, 8, 9, 4, ...],
  [2, 9, 8, 4, 5, 5, 3, 5, 6, 8, 9, 9, 7, 4, 5, 8, 9, 6, ...],
  [9, 8, 7, 3, 2, 1, 2, 7, 7, 9, 9, 9, 8, 5, 6, 9, 8, ...],
  [9, 7, 6, 2, 1, 0, 2, 3, 8, 9, 9, 8, 7, 6, 7, 9, ...],
  [8, 7, 4, 3, 2, 1, 2, 3, 8, 7, 8, 9, 8, 7, 8, ...],
  [7, 6, 5, 6, 3, 2, 6, 4, 5, 6, 8, 9, 9, 8, ...],
  [8, 7, 6, 6, 5, 4, 6, 5, 6, 7, 8, 9, 9, ...],
  [9, 8, 9, 8, 7, 5, 7, 8, 7, 8, 9, 4, ...],
  [9, 9, 9, 9, 8, 6, 8, 9, 9, 9, 2, ...],
  [8, 7, 9, 9, 9, 7, 8, 9, 2, 0, ...],
  [7, 6, 7, 8, 9, 8, 9, 3, 2, ...],
  [8, 5, 8, 9, 9, 9, 5, 4, ...],
  [5, 4, 6, 8, 9, 9, 7, ...],
  [6, 6, 7, 9, 6, 5, ...],
  [7, 8, 9, 2, 3, ...],
  [8, 9, 0, 1, ...],
  [9, 2, 1, ...],
  [4, 3, ...],
  [6, ...],
  [...],
  ...
]
```

## Part 1

These caves seem to be [lava tubes](https://en.wikipedia.org/wiki/Lava_tube). Parts are even still volcanically active; small hydrothermal vents release smoke into the caves that slowly settles like rain.

If you can model how the smoke flows through the caves, you might be able to avoid it and be that much safer. The submarine generates a heightmap of the floor of the nearby caves for you (your puzzle input).

Smoke flows to the lowest point of the area it's in. For example, consider the following heightmap:

```
2199943210
3987894921
9856789892
8767896789
9899965678
```

Each number corresponds to the height of a particular location, where `9` is the highest and `0` is the lowest a location can be.

Your first goal is to find the **low points** - the locations that are lower than any of its adjacent locations. Most locations have four adjacent locations (up, down, left, and right); locations on the edge or corner of the map have three or two adjacent locations, respectively. (Diagonal locations do not count as adjacent.)

In the above example, there are **four** low points, all highlighted: two are in the first row (a `1` and a `0`), one is in the third row (a `5`), and one is in the bottom row (also a `5`). All other locations on the heightmap have some lower adjacent location, and so are not low points.

The **risk level** of a low point is **1 plus its height**. In the above example, the risk levels of the low points are `2`, `1`, `6`, and `6`. The sum of the risk levels of all low points in the heightmap is therefore **`15`**.

Find all of the low points on your heightmap. **What is the sum of the risk levels of all low points on your heightmap?**

<!-- livebook:{"branch_parent_index":0} -->

## Solve: Part 1

```elixir
defmodule Day9.Heightmap do
  def size(heightmap) do
    width = heightmap |> List.first() |> Enum.count()
    height = heightmap |> Enum.count()

    {width, height}
  end

  def is_low_point?(heightmap, x, y, {width, height}) do
    point_value = get_value(heightmap, x, y)

    for {dx, dy} <- [{-1, 0}, {1, 0}, {0, -1}, {0, 1}],
        x + dx > -1,
        x + dx < width,
        y + dy > -1,
        y + dy < height do
      get_value(heightmap, x + dx, y + dy)
    end
    |> Enum.all?(&(&1 > point_value))
  end

  def risk_level(heightmap, x, y) do
    get_value(heightmap, x, y) + 1
  end

  defp get_value(heightmap, x, y) do
    heightmap |> Enum.at(y) |> Enum.at(x)
  end
end

alias Day9.Heightmap

{width, height} = size = Heightmap.size(inputs)

for x <- 0..(width - 1), y <- 0..(height - 1), Heightmap.is_low_point?(inputs, x, y, size) do
  Heightmap.risk_level(inputs, x, y)
end
|> Enum.sum()
```

```output
15
```

## Part 2

Next, you need to find the largest basins so you know what areas are most important to avoid.

A **basin** is all locations that eventually flow downward to a single low point. Therefore, every low point has a basin, although some basins are very small. Locations of height `9` do not count as being in any basin, and all other locations will always be part of exactly one basin.

The **size** of a basin is the number of locations within the basin, including the low point. The example above has four basins.

The top-left basin, size `3`:

```
2199943210
3987894921
9856789892
8767896789
9899965678
```

The top-right basin, size `9`:

```
2199943210
3987894921
9856789892
8767896789
9899965678
```

The middle basin, size `14`:

```
2199943210
3987894921
9856789892
8767896789
9899965678
```

The bottom-right basin, size `9`:

```
2199943210
3987894921
9856789892
8767896789
9899965678
```

Find the three largest basins and multiply their sizes together. In the above example, this is `9 * 14 * 9 = 1134`.

**What do you get if you multiply together the sizes of the three largest basins?**

<!-- livebook:{"branch_parent_index":0} -->

## Solve: Part 2

```elixir
defmodule Day9.Heightmap do
  def size(heightmap) do
    width = heightmap |> List.first() |> Enum.count()
    height = heightmap |> Enum.count()

    {width, height}
  end

  def is_low_point?(heightmap, {x, y}, size) do
    point_value = get_value(heightmap, {x, y})

    get_adjacent_points({x, y}, size)
    |> Enum.map(&get_value(heightmap, &1))
    |> Enum.all?(&(&1 > point_value))
  end

  def risk_level(heightmap, {x, y}) do
    get_value(heightmap, {x, y}) + 1
  end

  def calc_basin_size(heightmap, point, size) do
    finished_set = MapSet.new()
    not_finished_list = [point]

    do_calc_basin_size(heightmap, finished_set, not_finished_list, size)
  end

  def do_calc_basin_size(
        heightmap,
        finished_set,
        [current_point | not_finished_list],
        size
      ) do
    adjacent_points =
      get_adjacent_points(current_point, size)
      |> Enum.reject(fn point ->
        MapSet.member?(finished_set, point) || get_value(heightmap, point) == 9
      end)

    do_calc_basin_size(
      heightmap,
      MapSet.put(finished_set, current_point),
      Enum.uniq(not_finished_list ++ adjacent_points),
      size
    )
  end

  def do_calc_basin_size(_heightmap, finished_set, [], _size) do
    MapSet.size(finished_set)
  end

  defp get_adjacent_points({x, y}, size) do
    for {dx, dy} <- [{-1, 0}, {1, 0}, {0, -1}, {0, 1}], is_valid_point?({x + dx, y + dy}, size) do
      {x + dx, y + dy}
    end
  end

  defp is_valid_point?({x, y}, {width, height}) do
    x > -1 and x < width and y > -1 and y < height
  end

  defp get_value(heightmap, {x, y}) do
    heightmap |> Enum.at(y) |> Enum.at(x)
  end
end

alias Day9.Heightmap

{width, height} = size = Heightmap.size(inputs)

for x <- 0..(width - 1), y <- 0..(height - 1), Heightmap.is_low_point?(inputs, {x, y}, size) do
  Heightmap.calc_basin_size(inputs, {x, y}, size)
end
|> Enum.sort(:desc)
|> Enum.take(3)
|> Enum.reduce(1, &(&1 * &2))
```

```output
1076922
```
