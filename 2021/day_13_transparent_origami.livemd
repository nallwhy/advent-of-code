<!-- livebook:{"persist_outputs":true} -->

# Day 13: Transparent Origami

## Setup

```elixir
Mix.install([:vega_lite, :kino])

alias VegaLite, as: Vl
```

```output
VegaLite
```

```elixir
[dots_input, folds_input] =
  File.read!("./2021/day_13_input.txt")
  |> String.split("\n\n", trim: true)
  |> Enum.map(&String.split(&1, "\n", trim: true))

dots =
  dots_input
  |> Enum.map(fn dots_str ->
    String.split(dots_str, ",")
    |> Enum.map(&String.to_integer/1)
    |> List.to_tuple()
  end)
  |> MapSet.new()

folds =
  folds_input
  |> Enum.map(fn "fold along " <> fold_str ->
    [axis, value_str] = String.split(fold_str, "=")

    [axis, String.to_integer(value_str)]
  end)

{dots, folds}
```

```output
{#MapSet<[
   {935, 683},
   {387, 366},
   {1233, 456},
   {85, 550},
   {26, 201},
   {498, 227},
   {246, 339},
   {80, 779},
   {175, 150},
   {293, 135},
   {271, 585},
   {211, 459},
   {956, 854},
   {629, 409},
   {408, 555},
   {589, 729},
   {264, 618},
   {1134, 695},
   {938, 215},
   {708, 42},
   {694, 546},
   {35, 642},
   {251, 271},
   {1114, 362},
   {428, 571},
   {991, 686},
   {902, 772},
   {848, 693},
   {383, 467},
   {708, 33},
   {863, 728},
   {827, 880},
   {1099, 435},
   {1052, 4},
   {336, 631},
   {504, 39},
   {1225, 792},
   {493, 514},
   {415, 862},
   {1285, 485},
   {423, 250},
   {510, 500},
   {857, 47},
   {1173, 460},
   {893, 226},
   {283, 0},
   {951, 537},
   {293, ...},
   {...},
   ...
 ]>,
 [
   ["x", 655],
   ["y", 447],
   ["x", 327],
   ["y", 223],
   ["x", 163],
   ["y", 111],
   ["x", 81],
   ["y", 55],
   ["x", 40],
   ["y", 27],
   ["y", 13],
   ["y", 6]
 ]}
```

## Part 1

You reach another volcanically active part of the cave. It would be nice if you could do some kind of thermal imaging so you could tell ahead of time which caves are too hot to safely enter.

Fortunately, the submarine seems to be equipped with a thermal camera! When you activate it, you are greeted with:

```
Congratulations on your purchase! To activate this infrared thermal imaging
camera system, please enter the code found on page 1 of the manual.
```

Apparently, the Elves have never used this feature. To your surprise, you manage to find the manual; as you go to open it, page 1 falls out. It's a large sheet of [transparent paper](https://en.wikipedia.org/wiki/Transparency_(projection))! The transparent paper is marked with random dots and includes instructions on how to fold it up (your puzzle input). For example:

```
6,10
0,14
9,10
0,3
10,4
4,11
6,0
6,12
4,1
0,13
10,12
3,4
3,0
8,4
1,10
2,14
8,10
9,0

fold along y=7
fold along x=5
```

The first section is a list of dots on the transparent paper. `0,0` represents the top-left coordinate. The first value, `x`, increases to the right. The second value, `y`, increases downward. So, the coordinate `3,0` is to the right of `0,0`, and the coordinate `0,7` is below `0,0`. The coordinates in this example form the following pattern, where `#` is a dot on the paper and `.` is an empty, unmarked position:

```
...#..#..#.
....#......
...........
#..........
...#....#.#
...........
...........
...........
...........
...........
.#....#.##.
....#......
......#...#
#..........
#.#........
```

Then, there is a list of **fold instructions**. Each instruction indicates a line on the transparent paper and wants you to fold the paper **up** (for horizontal `y=...` lines) or **left** (for vertical `x=...` lines). In this example, the first fold instruction is `fold along y=7`, which designates the line formed by all of the positions where `y` is `7` (marked here with `-`):

```
...#..#..#.
....#......
...........
#..........
...#....#.#
...........
...........
-----------
...........
...........
.#....#.##.
....#......
......#...#
#..........
#.#........
```

Because this is a horizontal line, fold the bottom half **up**. Some of the dots might end up overlapping after the fold is complete, but dots will never appear exactly on a fold line. The result of doing this fold looks like this:

```
#.##..#..#.
#...#......
......#...#
#...#......
.#.#..#.###
...........
...........
```

Now, only `17` dots are visible.

Notice, for example, the two dots in the bottom left corner before the transparent paper is folded; after the fold is complete, those dots appear in the top left corner (at `0,0` and `0,1`). Because the paper is transparent, the dot just below them in the result (at `0,3`) remains visible, as it can be seen through the transparent paper.

Also notice that some dots can end up **overlapping**; in this case, the dots merge together and become a single dot.

The second fold instruction is `fold along x=5`, which indicates this line:

```
#.##.|#..#.
#...#|.....
.....|#...#
#...#|.....
.#.#.|#.###
.....|.....
.....|.....
```

Because this is a vertical line, fold left:

```
#####
#...#
#...#
#...#
#####
.....
.....
```

The instructions made a square!

The transparent paper is pretty big, so for now, focus on just completing the first fold. After the first fold in the example above, `17` dots are visible - dots that end up overlapping after the fold is completed count as a single dot.

**How many dots are visible after completing just the first fold instruction on your transparent paper?**

## Solve: Part 1

```elixir
defmodule Day13.Fold do
  def run(dots, folds) do
    do_run(dots, folds)
  end

  defp do_run(dots, []) do
    dots
  end

  defp do_run(dots, [fold | rest_folds]) do
    new_dots =
      dots
      |> Enum.reject(&is_on_line?(&1, fold))
      |> Enum.map(&fold(&1, fold))
      |> MapSet.new()

    do_run(new_dots, rest_folds)
  end

  defp is_on_line?({x, _y}, ["x", x]), do: true
  defp is_on_line?({_x, y}, ["y", y]), do: true
  defp is_on_line?(_dot, _fold), do: false

  defp fold({x, y}, ["x", fold_x]) do
    {fold_x - abs(x - fold_x), y}
  end

  defp fold({x, y}, ["y", fold_y]) do
    {x, fold_y - abs(y - fold_y)}
  end
end

alias Day13.Fold

Fold.run(dots, folds |> Enum.take(1))
|> Enum.count()
```

```output
675
```

## Part 2

Finish folding the transparent paper according to the instructions. The manual says the code is always **eight capital letters**.

**What code do you use to activate the infrared thermal imaging camera system?**

## Solve: Part 2

```elixir
values =
  Fold.run(dots, folds)
  |> Enum.map(fn {x, y} -> %{"x" => x, "y" => y} end)

Vl.new(width: 800, height: 200)
|> Vl.param("")
|> Vl.data_from_values(values)
|> Vl.mark(:rect)
|> Vl.encode_field(:x, "x")
|> Vl.encode_field(:y, "y")
|> Vl.encode(:color, aggregate: :count)
|> Vl.config(view: [stroke: nil])
```
