# Day 1: Trebuchet?!

```elixir
Mix.install([:kino])

input = Kino.Input.textarea("Please paste your input:")
```

## Root

```elixir
data =
  input
  |> Kino.Input.read()
  |> String.split("\n", trim: true)
```

<!-- livebook:{"branch_parent_index":0} -->

## Part 1

[![Run in Livebook](https://livebook.dev/badge/v1/blue.svg)](https://livebook.dev/run?url=https%3A%2F%2Fgithub.com%2Fnallwhy%2Fadvent-of-code%2Fblob%2Fmain%2F2023%2Fday_01.livemd)

Something is wrong with global snow production, and you've been selected to take a look. The Elves have even given you a map; on it, they've used stars to mark the top fifty locations that are likely to be having problems.

You've been doing this long enough to know that to restore snow operations, you need to check all fifty stars by December 25th.

Collect stars by solving puzzles. Two puzzles will be made available on each day in the Advent calendar; the second puzzle is unlocked when you complete the first. Each puzzle grants one star. Good luck!

You try to ask why they can't just use a [weather machine](https://adventofcode.com/2015/day/1) ("not powerful enough") and where they're even sending you ("the sky") and why your map looks mostly blank ("you sure ask a lot of questions") and hang on did you just say the sky ("of course, where do you think snow comes from") when you realize that the Elves are already loading you into a [trebuchet](https://en.wikipedia.org/wiki/Trebuchet) ("please hold still, we need to strap you in").

As they're making the final adjustments, they discover that their calibration document (your puzzle input) has been amended by a very young Elf who was apparently just excited to show off her art skills. Consequently, the Elves are having trouble reading the values on the document.

The newly-improved calibration document consists of lines of text; each line originally contained a specific calibration value that the Elves now need to recover. On each line, the calibration value can be found by combining the first digit and the last digit (in that order) to form a single two-digit number.

For example:

```

1abc2

pqr3stu8vwx

a1b2c3d4e5f

treb7uchet

```

In this example, the calibration values of these four lines are `12`, `38`, `15`, and `77`. Adding these together produces `142`.

Consider your entire calibration document. What is the sum of all of the calibration values?

```elixir
data
|> Enum.map(fn calibration ->
  Regex.replace(~r/[^\d]/, calibration, "")
  |> String.codepoints()
  |> then(&[List.first(&1), List.last(&1)])
  |> Enum.join()
  |> String.to_integer()
end)
|> Enum.sum()
```

<!-- livebook:{"branch_parent_index":0} -->

## Part 2

Your calculation isn't quite right. It looks like some of the digits are actually spelled out with letters: `one`, `two`, `three`, `four`, `five`, `six`, `seven`, `eight`, and `nine` also count as valid "digits".

Equipped with this new information, you now need to find the real first and last digit on each line. For example:

```
two1nine
eightwothree
abcone2threexyz
xtwone3four
4nineeightseven2
zoneight234
7pqrstsixteen

```

In this example, the calibration values are `29`, `83`, `13`, `24`, `42`, `14`, and `76`. Adding these together produces `281`.

What is the sum of all of the calibration values?

```elixir
defmodule Day01.Part2 do
  def run(data) do
    data
    |> Enum.map(fn calibration ->
      do_run(calibration, [])
      |> Enum.reverse()
      |> then(&([List.first(&1), List.last(&1)] |> Enum.join() |> String.to_integer()))
    end)
    |> Enum.sum()
  end

  defp do_run("", result) do
    result
  end

  [
    {"one", 1},
    {"two", 2},
    {"three", 3},
    {"four", 4},
    {"five", 5},
    {"six", 6},
    {"seven", 7},
    {"eight", 8},
    {"nine", 9}
  ]
  |> Enum.map(fn {number_string, number} ->
    defp do_run(unquote(number_string) <> _rest = string, result) do
      do_run(remove_first_letter(string), [unquote(number) | result])
    end

    defp do_run(unquote(number |> Integer.to_string()) <> _rest = string, result) do
      do_run(remove_first_letter(string), [unquote(number) | result])
    end
  end)

  defp do_run(not_matched_string, result) do
    do_run(remove_first_letter(not_matched_string), result)
  end

  defp remove_first_letter(<<_::utf8>> <> rest) do
    rest
  end
end

Day01.Part2.run(data)
```
